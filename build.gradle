buildscript {
    ext {
        springCloudVersion = "2023.0.3"
        springBootVersion = '3.3.5'
        springData = '2024.1.0'
        prometheusVersion = '1.14.1'
        commonsLang3 = '3.17.0'
        commonIo = '2.17.0'
        modelMapper = '3.2.1'
        openApiSwagger = '2.1.6'
        commonValidation = '1.9.0'
        lombokVerison = '1.18.36'
    }
}

plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.5' apply false
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.graalvm.buildtools.native' version '0.10.3'
    id "pl.allegro.tech.build.axion-release" version "1.18.7"
}

scmVersion {
    versionIncrementer('incrementPatch')
    ignoreUncommittedChanges.set(false)
    tag {
        versionIncrementer('incrementPatch')

        prefix.set('v')

    }
}

group = 'm7011e.the-homeric-odyssey'
project.version = scmVersion.version + '13'

subprojects.each {
    check.dependsOn("${it.name}:check")
    publish.dependsOn("${it.name}:publish")
    assemble.dependsOn("${it.name}:assemble")
}

subprojects {
    group = rootProject.group
    project.version = rootProject.version
}

configure(
        subprojects.findAll {
            !(['common-dependencies'].contains(it.name))
        }) {

    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.graalvm.buildtools.native'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }


    dependencies {
        api(group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: springBootVersion)
        api(group: 'org.springframework.data', name: 'spring-data-bom', version: springData, ext: 'pom')
        api(group: 'org.springframework.cloud', name: 'spring-cloud-dependencies', version: springCloudVersion)
        api(group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: prometheusVersion)
        api(group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3)
        api(group: 'commons-io', name: 'commons-io', version: commonIo)
        api(group: 'org.modelmapper', name: 'modelmapper', version: modelMapper)
        api(group: 'io.openapitools.swagger', name: 'swagger-maven-plugin', version: openApiSwagger)
        api(group: 'commons-validator', name: 'commons-validator', version: commonValidation)
        api(group: 'org.projectlombok', name: 'lombok', version: lombokVerison)

        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-data-rest'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
        implementation 'org.apache.commons:commons-lang3:3.17.0'

        compileOnly 'org.projectlombok:lombok'
        compileOnly group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: '3.3.5', ext: 'pom'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        }
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    tasks.named('processAot') {
        enabled = false
    }

    tasks.register('sourceJar', org.gradle.jvm.tasks.Jar) {
        from sourceSets.main.allJava
        archiveClassifier = "sources"
    }

    publishing {
        repositories {
            maven {
                allowInsecureProtocol = true
                if (project.version.toString().contains('-SNAPSHOT')) {
                    url("${reposiliteUrl}/snapshots")
                } else {
                    url("${reposiliteUrl}/releases")
                }
                name "Reposilite"
                credentials(PasswordCredentials) {
                    username = reposiliteUser
                    password = reposiliteSecret
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourceJar
            }
        }
    }
}


allprojects {
    project.version = rootProject.version
    group = rootProject.group

    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
        maven {
            allowInsecureProtocol = true
            url("${reposiliteUrl}/snapshots")
            name "Reposilite"
            credentials(PasswordCredentials) {
                username = reposiliteUser
                password = reposiliteSecret
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
        maven {
            allowInsecureProtocol = true
            url("${reposiliteUrl}/releases")
            name "Reposilite"
            credentials(PasswordCredentials) {
                username = reposiliteUser
                password = reposiliteSecret
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

}

project(':resource-server') {
    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.apache.groovy:groovy:4.0.15'
        testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
        testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
        testImplementation 'net.bytebuddy:byte-buddy:1.14.9'
        testImplementation 'com.athaydes:spock-reports:2.5.1-groovy-4.0'
    }
}

project(':authentication-components') {
    dependencies {
        api(project(':resource-server'))

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.apache.groovy:groovy:4.0.15'
        testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
        testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
        testImplementation 'net.bytebuddy:byte-buddy:1.14.9'
        testImplementation 'com.athaydes:spock-reports:2.5.1-groovy-4.0'
    }
}


tasks.named('test') {
    useJUnitPlatform()
}



